<?xml version="1.0" encoding="utf-8"?>
<s:SkinnablePopUpContainer xmlns:fx="http://ns.adobe.com/mxml/2009" 
						   xmlns:s="library://ns.adobe.com/flex/spark" 
						   backgroundAlpha="1" backgroundColor="0xFFFFFF"  
						   resize="resizeHandler(event)" 
						   open="initHandler()">
	<fx:Script>
		<![CDATA[
			import com.component.iconbutton.ImageButton;
			import com.util.AppEvent;
			
			import flash.utils.setTimeout;
			
			import mx.collections.ArrayCollection;
			import mx.events.ResizeEvent;
			
			private const ROOTSTATE:String = "RootState";
			private const SUBSTATE:String = "SubState";
			private const LOADSTATE:String = "LoadState";
			
			[Bindable]
			private var dm:DataManager = DataManager.getInstance();
			/**相片项目宽度-PhotoView*/
			public var dirItemWidth:Number = 148 * dm.multiResolution;
			/**相片项目高度-PhotoView*/
			public var dirItemHeight:Number = 148 * dm.multiResolution;
			
			/**获取文件目录信息*/
			private function getDirInfo(f:File):void
			{
				if(f != null && f.exists && f.isDirectory)
				{
					this.currentState = LOADSTATE;
					if (dm.customMbTilesDir != null) 
					{
						this.closeDirInfo([dm.customMbTilesDir]);
					}
					dm.customMbTilesDir = f;
					f.getDirectoryListingAsync();
					f.addEventListener(FileListEvent.DIRECTORY_LISTING, dirListHandler); 
				}
			}
			
			/**终端文件目录信息获取*/
			private function closeDirInfo(fs:Array):void
			{
				if(fs != null && fs.length > 0)
				{ 
					for each(var f:File in fs)
					{
						if(f != null && f.hasEventListener(FileListEvent.DIRECTORY_LISTING))
						{
							f.removeEventListener(FileListEvent.DIRECTORY_LISTING, dirListHandler); 
						}
					}
				}
			}
			
			/**设置本地图片数据源-PhotoView, PhotoGroupView*/
			public function dirGenerator(tileGroup:TileGroup, dirCol:ArrayCollection, fn:Function=null):void
			{
				tileGroup.removeAllElements();
				if(dirCol != null && dirCol.length > 0)
				{
					for each (var f:File in dirCol) 
					{
						var temp:Array = f.nativePath.split("/");
						var dirName:String = temp[temp.length-1];
						if(f.isDirectory)
						{
							var dirGroup:ImageButton = new ImageButton();
							dirGroup.url = dm.iconAssembly.dirIcon;
							dirGroup.text = dirName;
							dirGroup.width = dirItemWidth;
							dirGroup.height = dirItemHeight;
							dirGroup.param = f;
							if(fn != null)
							{
								dirGroup.addEventListener(MouseEvent.CLICK, fn);
							}
							tileGroup.addElement(dirGroup);
						}
						else
						{
							var fileItem:ImageButton = new ImageButton();
							fileItem.url = dm.iconAssembly.fileIcon;
							fileItem.text = dirName;
							fileItem.width = dirItemWidth;
							fileItem.height = dirItemHeight;
							tileGroup.addElement(fileItem);
						}
					}
				}
			}
			
			protected function initHandler():void
			{
				var systemConfigPath:String = dm.getSystemConfigMbtilesPath();
				currentDirPath.text = systemConfigPath;
				var systemConfigFile:File = File.documentsDirectory.resolvePath(systemConfigPath)
				this.getDirInfo(systemConfigFile);
			}
			
			/**点击文件夹图标*/
			private function dirGroupClickHandler(event:MouseEvent):void
			{
				var imgBtn:ImageButton = event.currentTarget as ImageButton;
				if(imgBtn != null && imgBtn.param != null)
				{
					var f:File = imgBtn.param as File;
					this.getDirInfo(f);
				}
			}
			
			private function dirListHandler(event:FileListEvent):void 
			{ 
				var rootDir:Array = event.files; 
				this.dirGenerator(dirGroup, new ArrayCollection(rootDir), dirGroupClickHandler);
				
				this.closeDirInfo([dm.customMbTilesDir]);
				
				//实时刷新列表
				//				dm.findOfflineMap(dm.customMbTilesDir);
				
				this.checkDirPanelState();
			}
			
			/**计算行数*/
			private function calcuteColumnCount():void
			{
				this.dirScroller.width = this.width - 10;
				var dirGroupWidth:Number = this.width - 10 - 20;
				dirGroup.width = dirGroupWidth;
				var colCount:int = parseInt((dirGroupWidth/dirItemWidth).toString());
				var gapWidth:Number = (colCount-1)*40;
				var realWidth:Number = dirGroupWidth - gapWidth;
				var realColCount:int = parseInt((realWidth/dirItemWidth).toString());
				dirGroup.requestedColumnCount = realColCount;
			}
			
			protected function resizeHandler(event:ResizeEvent):void
			{
				// TODO Auto-generated method stub
				this.calcuteColumnCount();
			}
			
			protected function resetBtn_clickHandler(event:MouseEvent):void
			{
				getDirInfo(File.documentsDirectory);
			}
			
			private function checkDirPanelState():void
			{
				this.currentDirPath.text = dm.customMbTilesDir.nativePath;
				var temp:Array = dm.customMbTilesDir.nativePath.split("/");
				var dirName:String = temp[temp.length-1];
				if(dirName == "")
				{
					this.currentState = ROOTSTATE;
				}
				else
				{
					this.currentState = SUBSTATE;
				}
			}
			
			protected function returnLastDir_clickHandler(event:MouseEvent):void
			{
				if(dm.customMbTilesDir != null && dm.customMbTilesDir.parent != null)
				{
					var f:File = dm.customMbTilesDir.parent;
					this.getDirInfo(f);
				}
			}
			
			protected function confirmBtn_clickHandler(event:MouseEvent):void
			{
				if(dm.customMbTilesDir != null)
				{
					var path:String = dm.customMbTilesDir.nativePath;
					var temp:Array = path.split(File.separator);
					if (temp[temp.length-1] != "") {
						//更新数据库存储文件路径
						var success:Boolean = dm.setSystemConfigMbtilesPath(path);
						toolTipLb.visible = true;
						if (success == false) {
							toolTipLb.text = "更新路径失败!";
						} else {
							toolTipLb.text = "更新路径成功!";
							AppEvent.dispatch(AppEvent.MBTILES_PATH_CHANGE);
						}
						setTimeout(hideToolTipHandler,2000);
					}
				}
			}
			
			private function hideToolTipHandler():void
			{
				toolTipLb.visible = toolTipLb.includeInLayout = false;
				toolTipLb.text = "";
			}
			
		]]>
	</fx:Script>
	<s:states>
		<s:State name="RootState"/>
		<s:State name="SubState"/>
		<s:State name="LoadState"/>
	</s:states>
	<s:VGroup width="100%" height="100%" verticalAlign="top" horizontalAlign="center"
			  paddingLeft="5" paddingRight="5" paddingTop="5" paddingBottom="5">
		<s:HGroup width="100%" height="{48*dm.multiResolution}" gap="0">
			<s:Label text="当前目录：" height="100%" verticalAlign="middle"/>
			<s:Label id="currentDirPath" text="请选择离线地图存放目录" 
					 verticalAlign="middle" 
					 width="100%" height="100%"/>
			<s:Button id="confirmBtn" label="确定目录" width="{124*dm.multiResolution}" height="100%"
					  click="confirmBtn_clickHandler(event)"/>
		</s:HGroup>
		<s:Rect width="100%" height="2">
			<s:fill>
				<s:SolidColor color="#000000"/>
			</s:fill>
		</s:Rect>
		<s:HGroup width="100%" height="{48*dm.multiResolution}" 
				   verticalAlign="middle" includeIn="SubState" itemCreationPolicy="immediate">
			<s:Label id="toolTipLb" height="100%" visible="false" includeInLayout="{toolTipLb.visible}"/>
			<s:Spacer width="100%"/>
			<s:Button id="resetBtn" label="返回根目录"  
					  icon="{dm.iconAssembly.resetIcon}" 
					  iconPlacement="left" 
					  width="{192*dm.multiResolution}" height="100%"
					  click="resetBtn_clickHandler(event)"/>
			<s:Button id="returnLastDir" label="返回上一级" 
					  icon="{dm.iconAssembly.backIcon}" 
					  iconPlacement="left" 
					  width="{192*dm.multiResolution}" height="100%"
					  click="returnLastDir_clickHandler(event)"/>
		</s:HGroup>
		<s:Scroller id="dirScroller" height="100%" excludeFrom="LoadState"
					horizontalScrollPolicy="off" verticalScrollPolicy="on">
			<s:VGroup width="100%" height="100%" gap="0"
					  horizontalAlign="center"
					  verticalAlign="top">
				<s:TileGroup id="dirGroup" 
							 paddingTop="10" paddingBottom="10" 
							 paddingLeft="10" paddingRight="10"
							 horizontalGap="40" verticalGap="40"
							 width="100%" height="100%"
							 horizontalAlign="center" 
							 verticalAlign="middle"
							 requestedRowCount="-1" 
							 requestedColumnCount="-1">
				</s:TileGroup>
			</s:VGroup>
		</s:Scroller>
		<s:VGroup width="100%" height="100%" horizontalAlign="center" verticalAlign="middle" includeIn="LoadState">
			<s:BusyIndicator symbolColor="#ff0000" width="{72*dm.multiResolution}" height="{72*dm.multiResolution}"/>
			<s:Label text="正在查询，请稍候..." width="100%" textAlign="center"/>
		</s:VGroup>
	</s:VGroup>
</s:SkinnablePopUpContainer>
